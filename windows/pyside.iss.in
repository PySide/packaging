; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{45E0A4C1-4682-4F5E-9536-0E7DEB431BF0}
AppName=PySide
AppVerName=PySide @PYSIDE_VERSION@
AppVersion=@PYSIDE_VERSION@
AppPublisher=PySide Team
AppPublisherURL=http://www.pyside.org
AppSupportURL=http://www.pyside.org
AppUpdatesURL=http://www.pyside.org
AppComments=Python bindings for Qt
AppCopyright=Copyright (C) 2010 Nokia Corporation and/or its subsidiary(-ies).
AppContact=contact@pyside.org
CreateAppDir=no
LicenseFile=@SOURCE_DIR@\pyside\COPYING
OutputBaseFilename=pyside-@PYSIDE_VERSION@-py@PYTHON_VERSION@
Compression=lzma
SolidCompression=yes

[Files]
; Required libraries
Source: "@INSTALL_DIR@\bin\shiboken.dll"; DestDir: "{sys}"; Flags: confirmoverwrite sharedfile ignoreversion
Source: "@INSTALL_DIR@\bin\pyside.dll"; DestDir: "{sys}"; Flags: confirmoverwrite sharedfile ignoreversion
; Python modules
Source: "@INSTALL_DIR@\lib\site-packages\PySide\*"; DestDir: "{reg:HKLM\Software\Python\PythonCore\@PYTHON_VERSION@\InstallPath,|Foo}Lib\site-packages\PySide"; Flags: confirmoverwrite ignoreversion

[Code]
var
  PythonPath : String;

function InitializeSetup(): Boolean;
begin
  Result := RegQueryStringValue(HKEY_LOCAL_MACHINE, 'Software\Python\PythonCore\@PYTHON_VERSION@\InstallPath', '', PythonPath);
  if (not Result) then
    MsgBox('PySide requires Python @PYTHON_VERSION@ and it was not found on your system.'#13#10'Install it and try again.', mbError, MB_OK);
end;

function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo,
  MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
var
  S: String;
  sys : String;
  pyDir : String;
begin
  sys := ExpandConstant('{sys}');
  RegQueryStringValue(HKEY_LOCAL_MACHINE, 'Software\Python\PythonCore\@PYTHON_VERSION@\InstallPath', '', pyDir);
  S := '';
  S := S + 'Python modules will be installed on::' + NewLine;
  S := S + Space + pyDir + 'Lib\site-packages\PySide' + NewLine;
  S := S + NewLine;

  S := S + 'The following support libraries will be installed on foo:' + NewLine;
  S := S + Space + sys + '\shiboken.dll' + NewLine;
  S := S + Space + sys + '\pyside.dll' + NewLine;

  Result := S;
end;
